<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
                layout="absolute"
                width="100%"
                height="100%">
  <mx:Panel title="Layer Definition"
            width="100%"
            height="100%">
    <mx:UIComponent id="mapHolder"
                    creationComplete="onHolderCreated(event)"
                    width="100%"
                    height="100%"/>
  </mx:Panel>

  <mx:Script>
    <![CDATA[
      import com.google.maps.LatLngBounds;
      import com.google.maps.controls.NavigationControl;
      import com.google.maps.controls.MapTypeControl;

      import com.google.maps.LatLng;
      import com.google.maps.Map;
      import com.google.maps.MapEvent;
      import com.google.maps.MapType;
      import com.google.maps.MapOptions;
      import com.google.maps.interfaces.*;

      import ags.*;

      private var map:Map;
      private var tiled:ArcGISMapService;

      public function onHolderCreated(event:Event):void {
        tiled=new ArcGISMapService('http://server.arcgisonline.com/ArcGIS/rest/services/ESRI_StreetMap_World_2D/MapServer');
        tiled.addEventListener(ArcGISEvent.LOAD, onServiceLoad);
      }

      public function onServiceLoad(event:ArcGISEvent):void {
        // map should be constructed after service load instead from MXML, if you do not want load Google Tiles at all. 
        map=new Map();
        map.width=mapHolder.width;
        map.height=mapHolder.height;
        map.key="ABQIAAAA7QUChpcnvnmXxsjC7s1fCxQGj0PqsCtxKvarsoS-iqLdqZSKfxTd7Xf-2rEc_PC9o8IsJde80Wnj4g";
        map.addEventListener(MapEvent.MAP_PREINITIALIZE, onMapPreinitialize);
        map.addEventListener(MapEvent.MAP_READY, onMapReady);
        mapHolder.addChild(map);
      }

      private function onMapPreinitialize(event:Event):void {
        var agsMapType:ArcGISMapType=new ArcGISMapType([new ArcGISTileLayer(tiled)], new ArcGISMapTypeOptions({name: tiled.name.replace(/ /g, '\n')}));
        var opts:MapOptions=new MapOptions();
        opts.zoom=4;
        opts.center=new LatLng(40, -95);
        opts.mapTypes=[agsMapType];
        map.setInitOptions(opts);
      }

      private function onMapReady(event:Event):void {
        map.addControl(new com.google.maps.controls.MapTypeControl());
        map.addControl(new NavigationControl());
        map.enableScrollWheelZoom();
        map.enableContinuousZoom();
        var bnds:LatLngBounds=new LatLngBounds(new LatLng(36.8,-103),new LatLng(40.2,-94));
        map.setCenter(bnds.getCenter(),map.getBoundsZoomLevel(bnds));
        var dynaLayer:ArcGISMapOverlay=new ArcGISMapOverlay("http://sampleserver1.arcgisonline.com/ArcGIS/rest/services/Demographics/ESRI_Census_USA/MapServer");
        dynaLayer.addEventListener(ArcGISEvent.LOAD, function(evt:Event):void {
            var service:ArcGISMapService=dynaLayer.getMapService();
            service.getLayer("Coarse Counties").definition="STATE_NAME='Kansas' and POP2007>25000";
            service.getLayer("Detailed Counties").definition="STATE_NAME='Kansas' and POP2007>25000";
            service.getLayer("states").definition="STATE_NAME='Kansas'";
            map.addOverlay(dynaLayer);

          });



      }
    ]]>
  </mx:Script>
</mx:Application>
