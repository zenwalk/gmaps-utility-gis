<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
                layout="absolute"
                width="100%"
                height="100%">
  <mx:Panel title="ArcGIS Online Maps. Check License restrictions at www.arcgisonline.com to use in production systems."
            width="100%"
            height="100%">
    <mx:UIComponent id="mapHolder"
                    creationComplete="onHolderCreated(event)"
                    width="100%"
                    height="100%"/>
  </mx:Panel>

  <mx:Script>
    <![CDATA[
      import com.google.maps.controls.NavigationControl;
      import com.google.maps.controls.MapTypeControl;

      import com.google.maps.LatLng;
      import com.google.maps.Map;
      import com.google.maps.MapEvent;
      import com.google.maps.MapType;
      import com.google.maps.MapOptions;
      import com.google.maps.interfaces.*;

      import ags.*;

      private var base:String='http://server.arcgisonline.com/ArcGIS/rest/services';
      private var map:Map;
      private var svc:ArcGISMapService;

      public function onHolderCreated(event:Event):void {
        svc=new ArcGISMapService(base + '/ESRI_StreetMap_World_2D/MapServer');
        svc.addEventListener(ArcGISEvent.LOAD, onServiceLoad);
      }

      public function onServiceLoad(event:ArcGISEvent):void {
        // map should be constructed after service load instead from MXML, if you do not want load Google Tiles at all. 
        map=new Map();
        map.width=mapHolder.width;
        map.height=mapHolder.height;
        map.key="ABQIAAAA7QUChpcnvnmXxsjC7s1fCxQGj0PqsCtxKvarsoS-iqLdqZSKfxTd7Xf-2rEc_PC9o8IsJde80Wnj4g";
        map.addEventListener(MapEvent.MAP_PREINITIALIZE, onMapPreinitialize);
        map.addEventListener(MapEvent.MAP_READY, onMapReady);
        mapHolder.addChild(map);
      }

      private function onMapPreinitialize(event:Event):void {
        var agsLayer:ArcGISTileLayer=new ArcGISTileLayer(svc);
        var agsMapType:ArcGISMapType=new ArcGISMapType([agsLayer], new ArcGISMapTypeOptions({name: agsLayer.getName().replace(/ /g, '\n')}));
        var opts:MapOptions=new MapOptions();
        opts.zoom=4;
        opts.center=new LatLng(40, -95);
        opts.mapTypes=[agsMapType];
        map.setInitOptions(opts);
      }

      private function onMapReady(event:Event):void {
        map.addControl(new com.google.maps.controls.MapTypeControl());
        map.addControl(new NavigationControl());
        map.enableScrollWheelZoom();
        map.enableContinuousZoom();
        ArcGISUtil.addArcGISMap(map, base + "/ESRI_Imagery_World_2D/MapServer");
        ArcGISUtil.addArcGISMap(map, base + "/ESRI_ShadedRelief_World_2D/MapServer");
        ArcGISUtil.addArcGISMap(map, base + "/I3_Imagery_Prime_World_2D/MapServer");
        ArcGISUtil.addArcGISMap(map, base + "/NGS_Topo_US_2D/MapServer");
        ArcGISUtil.addArcGISMap(map, base + "/NPS_Physical_World_2D/MapServer");
        ArcGISUtil.addArcGISMap(map, base + "/Demographics/ESRI_00-08PopGrwth_US_2D/MapServer");
        ArcGISUtil.addArcGISMap(map, base + "/Demographics/ESRI_Diversity_US_2D/MapServer");
        ArcGISUtil.addArcGISMap(map, base + "/Demographics/ESRI_MedianIncome_US_2D/MapServer");
        ArcGISUtil.addArcGISMap(map, base + "/Specialty/EVC_Topo_World_2D/MapServer");
      }
    ]]>
  </mx:Script>
</mx:Application>
